     A*****************************************************************
     A*               WEBVISITOR File Maintenance
     A*  Display WEBVISITD
     A*****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      PRINT
     A*****************************************************************
     A          R SFLS1                     SFL
     A  95                                  SFLNXTCHG
     A            ATBS1SEL       1A  P
     A            S1SELECT       1A  B 10  3
     A                                      DSPATR(&ATBS1SEL)
     A            S1VISITOR R        H      REFFLD(WEBVISITR/VISITOR WEBVISITOR)
     A            S1IPSEG1       3A  O 10  6
     A            S1IPSEG2       3A  O 10 10
     A            S1IPSEG3       3A  O 10 14
     A            S1IPSEG4       3A  O 10 18
     A            S1HOST        29A  O 10 22
     A            S1CITY        16A  O 10 52
     A            S1STATE        2A  O 10 69
     A            S1TOPIC   R        O 10 72REFFLD(WEBVISITR/TOPIC  WEBVISITOR)
     A*
     A*****************************************************************
     A          R SFLC1                     SFLCTL(SFLS1)
     A                                      SFLSIZ(0012)
     A                                      SFLPAG(0012)
     A                                      CF03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'Add')
     A                                      CF11(11 'View')
     A                                      CF12(12 'Return')
     A                                      ROLLDOWN(26 'ROLLDN/PAGEUP'  )
     A N94                                  ROLLUP(27 'ROLLUP/PAGEDOWN')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A  94                                  SFLEND(*MORE)
     A  96                                  CSRLOC(ROWPOS COLPOS)
     A*
     A            ROWPOS         3S 0H
     A            COLPOS         3S 0H
     A            SF1RRN         4S 0H
     A            PGMNAM        10A  O  1  2COLOR(BLU)
     A            C1FILE    R        O  1 35REFFLD(WEBVISITR/WEBSITE WEBVISITOR)
     A                                      COLOR(WHT)
     A                                  1 72DATE(*SYS *Y)
     A                                      EDTCDE(Y)
     A                                      COLOR(BLU)
     A                                  2  2USER
     A                                      COLOR(BLU)
     A            C1SITE    R        O  2 36REFFLD(WEBVISITR/WEBSITE WEBVISITOR)
     A                                      COLOR(WHT)
     A                                  2 72TIME
     A                                      COLOR(BLU)
     A                                  4  2'Position to'
     A            C1POSDSC       7A  O  4 14
     A  50        C1VISITOR R        B  4 22REFFLD(WEBVISITR/VISITOR WEBVISITOR)
     A  51        C1IPSEG1  R        B  4 22REFFLD(WEBVISITR/IPSEG1 WEBVISITOR)
     A  51        C1IPSEG2  R        B  4 27REFFLD(WEBVISITR/IPSEG2 WEBVISITOR)
     A  51        C1IPSEG3  R        B  4 32REFFLD(WEBVISITR/IPSEG3 WEBVISITOR)
     A  51        C1IPSEG4  R        B  4 37REFFLD(WEBVISITR/IPSEG4 WEBVISITOR)
     A  52        C1HOST    R        B  4 22REFFLD(WEBVISITR/HOST   WEBVISITOR)
     A                                      CHECK(LC)
     A  53        C1STATE   R        B  4 22REFFLD(WEBVISITR/STATE  WEBVISITOR)
     A            C1HOST3   R        H      REFFLD(WEBVISITR/HOST   WEBVISITOR)
     A  54        C1CITY    R        B  4 22REFFLD(WEBVISITR/CITY   WEBVISITOR)
     A                                      CHECK(LC)
     A            C1STATE4  R        H      REFFLD(WEBVISITR/STATE  WEBVISITOR)
     A            C1HOST4   R        H      REFFLD(WEBVISITR/HOST   WEBVISITOR)
     A  55        C1TOPIC   R        B  4 22REFFLD(WEBVISITR/TOPIC  WEBVISITOR)
     A                                      CHECK(LC)
     A            C1HOST5   R        H      REFFLD(WEBVISITR/HOST   WEBVISITOR)
     A*
     A                                  6  2'Type option, press Enter.'
     A                                      COLOR(BLU)
     A                                  7  5'2=Change'
     A                                      COLOR(BLU)
     A                                  7 16'4=Delete'
     A                                      COLOR(BLU)
     A                                  7 27'5=Display'
     A                                      COLOR(BLU)
     A*
     A                                  9  2'Opt'
     A                                      COLOR(WHT)
     A                                  9  6'IP1'
     A                                      COLOR(WHT)
     A                                  9 10'IP2'
     A                                      COLOR(WHT)
     A                                  9 14'IP3'
     A                                      COLOR(WHT)
     A                                  9 18'IP4'
     A                                      COLOR(WHT)
     A                                  9 22'Host'
     A                                      COLOR(WHT)
     A                                  9 52'City'
     A                                      COLOR(WHT)
     A                                  9 69'ST'
     A                                      COLOR(WHT)
     A                                  9 72'Topic'
     A                                      COLOR(WHT)
     A*
     A*****************************************************************
     A          R SFL1KEY
     A                                      OVERLAY
     A                                 23  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 23 12'F5=Refresh'
     A                                      COLOR(BLU)
     A                                 23 25'F6=Add'
     A                                      COLOR(BLU)
     A                                 23 34'F11=Change View'
     A                                      COLOR(BLU)
     A                                 23 52'F12=Return'
     A                                      COLOR(BLU)
     A*
     A*****************************************************************
     A          R SFLS2                     SFL
     A            S2SELECT       1A  B  4  4
     A            S2DESC         7A  O  4  7
     A                                      COLOR(WHT)
     A*
     A*****************************************************************
     A          R SFLC2                     SFLCTL(SFLS2)
     A                                      WINDOW(5 6 10 19)
     A                                      WDWBORDER((*COLOR YLW) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      SFLSIZ(0006)
     A                                      SFLPAG(0006)
     A                                      CF03(03 'Exit')
     A                                      CF05(05 'Refresh')
     A                                      CF06(06 'Add')
     A                                      CF11(11 'View')
     A                                      CF12(12 'Return')
     A                                      ROLLDOWN(26 'ROLLDN/PAGEUP'  )
     A                                      ROLLUP(27 'ROLLUP/PAGEDOWN')
     A                                      OVERLAY
     A  91                                  SFLDSP
     A  92                                  SFLDSPCTL
     A  93                                  SFLCLR
     A            SF2RRN         4S 0H
     A                                  2  4'1=Select View'
     A                                      COLOR(BLU)
     A*****************************************************************
     A          R MSGSFL                    SFL SFLMSGRCD(24)
     A            MSGKEY                    SFLMSGKEY
     A            PGMSGQ                    SFLPGMQ
     A*
     A*****************************************************************
     A          R MSGCTL                    SFLCTL(MSGSFL)
     A                                      OVERLAY
     A                                      SFLSIZ(0010)
     A                                      SFLPAG(0001)
     A                                      SFLDSP
     A                                      SFLDSPCTL
     A                                      SFLINZ
     A N98                                  SFLEND
     A            PGMSGQ                    SFLPGMQ
     A*****************************************************************
